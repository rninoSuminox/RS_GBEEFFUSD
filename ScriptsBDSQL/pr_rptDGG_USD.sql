/*  REPORTE Demostración de Gastos Generales
exec pr_rptDGG_USD @ANIO=2021,@MONEDA=N'USD',@EMPRESA=N'REFER'
exec pr_rptDGG_USD @ANIO=2020,@MONEDA=N'BSF',@EMPRESA=N'REFERVZLA'
exec pr_rptDGG_USD @ANIO=2020,@MONEDA=N'USD',@EMPRESA=N'sxper'
--se ejecuta en la BD principal, debe tener todas las cuentas contables.
// se realiza comentario para probar uso del repositorio el dia 02-06-2022
// se realizan cambios en la rama master 
// se realizan cambios de prueba de segunda jespidea jespidea
*/

if exists (select * from dbo.sysobjects where id = object_id(N'[dbo].[pr_rptDGG_USD]') and OBJECTPROPERTY(id, N'IsProcedure') = 1)
drop procedure [dbo].pr_rptDGG_USD
 go
CREATE  PROCEDURE pr_rptDGG_USD 
(@ANIO		INT
,@MONEDA	VARCHAR(6)='USD' --1:GUARDA EN TABLAS,2: TABLAS TEMP PARA REPORTE
,@EMPRESA   VARCHAR(100)
,@MESPERIODO INT=12)
AS
BEGIN

	DECLARE @TASA	   CHAR(15)=(SELECT TOP 1 TAXSCHID FROM BEF00200);  --TASA DE CONVERSION 'USD REPORTE'    

--EJECUTA SP, PARA LLENAR TABLA CON SALDOS y aplicar filtros
	exec pr_rptCuentasEmpresa_USD @ANIO=@ANIO,@MES=@MESPERIODO,@EMPRESA=@EMPRESA,@IDREPORTE='RPTDGG';
	--select * from TEMPSALDOS;
 print 'inicio';

 	 --AGRUPA LAS CUENTAS POR MES
	 WITH CTE_AGRUPO AS (	 
			 SELECT ANIO,MONTH(TRXDATE) MES,YEAR(TRXDATE) ANIOTRX
				   ,MAX(ACTDESCR) ACTDESCR
				   ,RTRIM(ACTNUMST) ACTNUMST
				   ,MAX(TRXDATE) TRXDATE
				   ,(SUM(SUM(SALDOBSF)) OVER (PARTITION BY ANIO,ACTNUMST ORDER BY ANIO,MONTH(TRXDATE))) ACUMBSF
			   FROM TEMPSALDOS
			  WHERE (LEFT(REPLACE(ACTNUMST,'-',''),5)='71103' OR  LEFT(REPLACE(ACTNUMST,'-',''),3)='714' OR LEFT(REPLACE(ACTNUMST,'-',''),3)='612') 
			  and REPLACE(ACTNUMST,'-','') NOT IN ('714013001','714013004','714011047')
			  GROUP BY ANIO,YEAR(TRXDATE),MONTH(TRXDATE),ACTNUMST),
	  CTE_ACUMULADO AS(
		SELECT *
		,ACUMBSF/dbo.f_buscatasadia(EOMONTH(TRXDATE),@TASA) -
		        ISNULL((LAG(ACUMBSF/dbo.f_buscatasadia(EOMONTH(TRXDATE),@TASA))
				       OVER (PARTITION BY ANIO,ACTNUMST ORDER BY ANIO,MES)),0)	 SALDOUSD
		,ACUMBSF -
		        ISNULL((LAG(ACUMBSF)
				       OVER (PARTITION BY ANIO,ACTNUMST ORDER BY ANIO,MES)),0)	SALDOBSF
		 FROM CTE_AGRUPO
	  ),
	 --AGRUPA LAS CUENTAS POR MES
	  CTE_INICIAL AS (
			 SELECT ANIO,MONTH(TRXDATE) MES,YEAR(TRXDATE) ANIOTRX,ACTNUMST
			 	   ,SUM(SALDOBSF) SALDOBSF
				   ,IIF(@MONEDA='USD',SUM(IIF(SALDOUSD=0,SALDOBSF/dbo.f_buscatasadia(TRXDATE,@TASA),SALDOUSD)),0) SALDOUSD --segun tasa del dia
					  ,MAX(ACTDESCR) ACTDESCR
			   FROM TEMPSALDOS
			   WHERE NOT(LEFT(REPLACE(ACTNUMST,'-',''),5)='71103' OR  LEFT(REPLACE(ACTNUMST,'-',''),3)='714' OR LEFT(REPLACE(ACTNUMST,'-',''),3)='612')
			  GROUP BY ANIO,YEAR(TRXDATE),MONTH(TRXDATE),ACTNUMST ),
	  CTE_SALDOS AS (
		  SELECT ANIO,MES,ANIOTRX,ACTNUMST,ACTDESCR,SALDOBSF,SALDOUSD FROM CTE_INICIAL 
		  UNION ALL 
		  SELECT ANIO,MES,ANIOTRX,ACTNUMST,ACTDESCR,SALDOBSF,SALDOUSD FROM CTE_ACUMULADO
	  ),
	  CTE_SEGMENTO AS ( -- PARA ARMAR LOS GRUPOS
		   SELECT 'GASTO PERSONAL'			 GRUPO,'711' SEGMENTO	UNION ALL
		   SELECT 'GASTOS FINANCIEROS'		 GRUPO,'712' SEGMENTO	UNION ALL
		   SELECT 'GASTOS DEL NEGOCIO'		 GRUPO,'713' SEGMENTO	UNION ALL
		   SELECT 'LIBERACIONES Y APARTADOS' GRUPO,'714' SEGMENTO	UNION ALL
		   SELECT 'LIBERACIONES Y APARTADOS' GRUPO,'612' SEGMENTO
	  ),
	  CTE_CTADETALLE AS ( --PARA SUBAGRUPAR POR CTAS DE DETALLE
		  SELECT '711-01-1002' DETALLE UNION ALL
		  SELECT '711-03-1003' DETALLE UNION ALL
		  SELECT '711-09-1007' DETALLE UNION ALL
		  SELECT '711-03-1008' DETALLE UNION ALL
		  SELECT '711-03-1020' DETALLE ),
	  CTE_SUBGRUPO AS (
		SELECT SGMNTID,RTRIM(DSCRIPTN) DSCRIPTN FROM BEF00100 WHERE LEFT(SGMNTID,3)IN ('711','712','713')
	  )

	   SELECT LEFT(ACTNUMST,CHARINDEX('-',ACTNUMST)-1) SEGMENTO
	         ,ACTNUMST
	         ,IIF(DETALLE IS NULL,ISNULL(DSCRIPTN,ACTDESCR),ACTDESCR) ACTDESCR 
			 --SI ES CUENTA DETALLE TRAE DESCRIPCION, SINO ES CTA DET, EVALUA SI EXISTE EL SEGMENTO
			 ,ANIO,MES
			 ,IIF(@MONEDA='USD',SALDOUSD,SALDOBSF)* -1 SALDO
			 ,GRUPO
			 ,ISNULL(DETALLE,LEFT(ACTNUMST,6)) SUBGRUPO
			 ,CASE 
			  WHEN REPLACE(S.ACTNUMST,'-','') IN ('711011002','711031003') THEN ''
			  WHEN LEFT(ACTNUMST,6) IN ('711-01','711-02','711-03','711-04','711-05','711-06') THEN 'GASTOS PERSONAL'
			  WHEN LEFT(ACTNUMST,3) IN ('711') THEN 'OTROS GASTOS PERSONAL'			  
			  ELSE '' END NIVELGRUPO
			  ,ANIOTRX
	   FROM CTE_SALDOS S
	  INNER JOIN CTE_SEGMENTO Z ON Z.SEGMENTO=LEFT(ACTNUMST,3) 
	   LEFT JOIN CTE_CTADETALLE T ON S.ACTNUMST=T.DETALLE
	   LEFT JOIN CTE_SUBGRUPO Y ON Y.SGMNTID=LEFT(REPLACE(ACTNUMST,'-',''),5) --SI NO CONSIGUE EL SEGMENTO TRAE LA DESCRIPCION DE CUENTA.
END