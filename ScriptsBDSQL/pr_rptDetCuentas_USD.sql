/*
exec pr_rptDetCuentas_USD @ANIO=2020,@MONEDA=N'USD',@EMPRESA=N'SXPER'
exec pr_rptDetCuentas_USD @ANIO=2020,@MONEDA=N'BSF',@EMPRESA=N'REFERVZLA'
exec pr_rptDetCuentas_USD @ANIO=2021,@MONEDA=N'USD',@EMPRESA=N'BALBS'
exec pr_rptDetCuentas_USD @ANIO=2020,@MONEDA=N'BSF',@EMPRESA=N'H2501'

--se ejecuta en la BD principal, debe tener todas las cuentas contables.
*/

if exists (select * from dbo.sysobjects where id = object_id(N'[dbo].[pr_rptDetCuentas_USD]') and OBJECTPROPERTY(id, N'IsProcedure') = 1)
drop procedure [dbo].pr_rptDetCuentas_USD
 go
CREATE  PROCEDURE pr_rptDetCuentas_USD 
(@ANIO		INT
,@MONEDA	VARCHAR(6)='USD' --1:GUARDA EN TABLAS,2: TABLAS TEMP PARA REPORTE
,@EMPRESA   VARCHAR(100)
,@MESPERIODO INT=12)
AS
BEGIN

	DECLARE @TASA	   CHAR(15)=(SELECT TOP 1 TAXSCHID FROM BEF00200); --TASA DE CONVERSION 'USD REPORTE'    

--EJECUTA SP, PARA LLENAR TABLA CON SALDOS y aplicar filtros
	exec pr_rptCuentasEmpresa_USD @ANIO=@ANIO,@MES=@MESPERIODO,@EMPRESA=@EMPRESA,@IDREPORTE='DETXMES';

	 --AGRUPA LAS CUENTAS POR MES
	 	 WITH CTE_AGRUPO AS (	 
			 SELECT ANIO,MONTH(TRXDATE) MES,YEAR(TRXDATE) ANIOTRX
				   ,MAX(ACTDESCR) ACTDESCR
				   ,RTRIM(ACTNUMST) ACTNUMST
				   ,MAX(TRXDATE) TRXDATE
				   ,(SUM(SUM(SALDOBSF)) OVER (PARTITION BY ANIO,ACTNUMST ORDER BY ANIO,MONTH(TRXDATE))) ACUMBSF
			   FROM TEMPSALDOS
			  WHERE (LEFT(REPLACE(ACTNUMST,'-',''),5)='71103' OR  LEFT(REPLACE(ACTNUMST,'-',''),3)='714' OR LEFT(REPLACE(ACTNUMST,'-',''),3)='612') 
			  and REPLACE(ACTNUMST,'-','') NOT IN ('714013001','714013004','714011047')
			  GROUP BY ANIO,YEAR(TRXDATE),MONTH(TRXDATE),ACTNUMST),
	  CTE_ACUMULADO AS(
		SELECT *
		,ACUMBSF/dbo.f_buscatasadia(EOMONTH(TRXDATE),@TASA) -
		        ISNULL((LAG(ACUMBSF/dbo.f_buscatasadia(EOMONTH(TRXDATE),@TASA))
				       OVER (PARTITION BY ANIO,ACTNUMST ORDER BY ANIO,MES)),0)	 SALDOUSD
		,ACUMBSF -
		        ISNULL((LAG(ACUMBSF)
				       OVER (PARTITION BY ANIO,ACTNUMST ORDER BY ANIO,MES)),0)	SALDOBSF
		 FROM CTE_AGRUPO
	  ),
	   CTE_SALDOS AS (
	 SELECT ANIO,MES,YEAR(TRXDATE) ANIOTRX --,ACTINDX
		       ,SUM(SALDOBSF) SALDOBSF
			   ,IIF(@MONEDA='USD',SUM(IIF(SALDOUSD=0,SALDOBSF/dbo.f_buscatasadia(TRXDATE,@TASA),SALDOUSD)),0) SALDOUSD --segun tasa del dia
			  ,MAX(ACTDESCR) ACTDESCR
			  ,ACTNUMST ACTNUMST
	   FROM TEMPSALDOS
	   WHERE  NOT(LEFT(REPLACE(ACTNUMST,'-',''),5)='71103' OR  LEFT(REPLACE(ACTNUMST,'-',''),3)='714' OR LEFT(REPLACE(ACTNUMST,'-',''),3)='612')
	  GROUP BY ANIO,YEAR(TRXDATE),MES,ACTNUMST
	  UNION ALL
	  	SELECT ANIO,MES,ANIOTRX,SALDOBSF,SALDOUSD,ACTDESCR,ACTNUMST
		FROM CTE_ACUMULADO
	  )
	   
	 -- SELECT * FROM CTE_SALDOS 
	   SELECT LEFT(ACTNUMST,CHARINDEX('-',ACTNUMST)-1) SEGMENTO,ACTNUMST
	         ,ACTDESCR,ANIO,MES
			 ,IIF(@MONEDA='USD',SALDOUSD,SALDOBSF)* -1 SALDO
			 ,ANIOTRX	
		FROM CTE_SALDOS S
END